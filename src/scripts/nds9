#! /bin/csh -f
#
#  send a (fits/nemo::ccd) file into ds9
#
#  Example to select a (new) frame:
#       xpaset -p ds9 frame frameno 2

if ($#argv == 0) then
  echo "Usage: $0 [-frameno] nemo|fits-file(s)"
  echo "Send NEMO/ccd or fits files into ds9 display server"
  echo "   (Notice you need to start 'ds9' before you use this command)"
  echo "   Optionally with -title foobar, with 'setenv DS9_TITLE foobar'"
  echo "Version:  2.2 3-jul-2019 PJT"
  exit 0
endif

set bad=0

if ($?DS9_TITLE) then
  set title=$DS9_TITLE
else
  set title=ds9
endif

foreach f ($*)
  # first check if an option is used
  set iso=`echo $f | cut -c1`
  if (X$iso == X-) then
    set frameno = `echo $f | cut -c2-`
    xpaset -p ds9 frame $frameno
    continue
  endif

  # if not, assume it's a filename
  hisf $f >& /dev/null
  if ($status) then
    set name=$f
    echo Assuming fits $name
    cat $name | xpaset $title fits $name
    set bad=$status
  else if (-e $f.fits) then
    set name=$f.fits
    echo Found fits $name
    cat $name | xpaset $title fits $name
    set bad=$status
  else if (-e $f) then
    echo Assuming NEMO file $f
    ccdfits $f $f.fits
    set name=$f.fits
    cat $name | xpaset $title fits $name
    set bad=$status
    rm -f $f.fits
  else
    echo $f or $f.fits does not exist
  endif
end

if ($bad) then
  echo "Problems with ds9/xpa ?"
  echo "You need to start the 'ds9' server before you use this command"
endif
