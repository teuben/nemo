#! /bin/csh
#
# this file is normally sourced from falcON_start
#
# $FALCON must be set.
#
if ($?FALCON == 0) then
  echo "Environment variable FALCON is not set, falcON cannot be set up"
  echo "add something like: setenv FALCON /xxx/yyy"
  echo "to denote the root directory of falcON to your .cshrc file"
  goto done
endif

# use FALCONLIB as indicator for falcON to be active
if($?FALCONLIB) then
  if(-d "$FALCONLIB") then
    echo "falcON had already been activated; falcON cannot be re-initilized"
    goto done
  endif
endif

# start nemo if it is not already present
if($?NEMO == 0) then
  set FALCON_REM = $FALCON
  if ($?prompt) then
    echo "trying to activate NEMO ..."
  endif
  nemo
  set FALCON = $FALCON_REM
endif

# set enviroment variables FALCONLIB and ACCPATH
setenv FALCONLIB $FALCON/lib
setenv WDUTILSLIB $FALCON/utils/lib

if($?ACCPATH) then
    setenv ACCPATH $FALCON"/acc/:"$ACCPATH
else
    setenv ACCPATH $FALCON"/acc/"
endif

# add FALCON/bin to PATH
setenv PATH $FALCON"/bin:"$PATH
rehash

if ($?prompt) then
  echo "falcON loaded from $FALCON"
endif

# set compiler
if($?MODULE_VERSION) then
  module purge
  module load gcc/4.7.0
endif

# add FALCONLIB to LD_LIBRARY_PATH and DYLD_LIBRARY_PATH
if ($?LD_LIBRARY_PATH) then
  setenv LD_LIBRARY_PATH ${FALCONLIB}":"${WDUTILSLIB}":"${LD_LIBRARY_PATH}
else
  setenv LD_LIBRARY_PATH ${FALCONLIB}":"${WDUTILSLIB}
endif
if($VENDOR == apple) then
  setenv DYLD_LIBRARY_PATH $LD_LIBRARY_PATH
endif

# provide alias 'NOclaf' to de-active falcON
alias NOclaf 'source $FALCON/falcON_end; unalias NOclaf'

done:

